#!/usr/bin/gnuplot -persist
reset
set terminal png
set term png size 1024,600 #800 pixels by 600 pixels

set datafile separator " "
set xdata time
set timefmt "%d/%m/%Y %H"
set xrange [time(0):time(0) + 5*24*60*60]
set format x "h%H\n%d/%m\n%a"
set xlabel "Time"
set xtics font ", 10"

set ytics 0.5
set yrange [0:12]
set ylabel 'mm/h'
set y2tics 10
set y2range [0:100]
set y2label '% Chance'

set title "Weather Forecast by Weather unlocked and Remo Tomasi (Rain/Snow)"
set key lmargin
set key font ",10"
set grid
set size 1, 1 # ratio 1:1

set style data lines
set boxwidth 0.95 relative
#set style fill transparent solid 0.5 noborder

plot "DATAS/tmp.csv" using 1:12 w boxes lc rgb "blue" fs solid border -1 title "PrMM" axes x1y1, \
2.5 lc rgb "#2F4F4F" lw 2 notitle, \
10 lc rgb "#2F4F4F" lw 2 notitle, \
"" using 1:13 w boxes lc rgb "green" fs solid border -1 title "SnMM" axes x1y1, \
"" using 1:14 w boxes lc rgb "yellow" fs solid border -1 title "SnCM" axes x1y1, \
"" using 1:10 lc "purple" title "RaMM", \
"" using 1:11 lc rgb "red" title "Pr%" axes x1y2, \
"" u 22:(95):(0.48):(90-0.01*$9*359):(90) w circ lc rgb "brown" fs solid notitle axes x2y2, \
"" u 22:(95):(0.48) w circ lc rgb "black" axes x2y2 t "Cloud"
#
