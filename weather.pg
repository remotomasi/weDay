#!/usr/bin/gnuplot -persist
reset
set terminal png
set term png size 1024,600 #800 pixels by 600 pixels
set angle degrees

set datafile separator " "
set xdata time
set timefmt "%d/%m/%Y %H"
set xrange [time(0):time(0) + 5*24*60*60]
#set format x "h%H\n%d/%m\n%a"
set xlabel "Time"
set xtics font ", 10"

set x2tics 10
set x2range [0:50]
set x2label 'Ore'

set ytics 0.5
set yrange [0:5]
set ylabel 'mm/h'
set y2tics 10
set y2range [0:100]
set y2label '% Chance'

set title "Weather Forecast by Weather unlocked and Remo Tomasi"
set key lmargin
set key font ",10"
set grid
set size 1, 1 # ratio 1:1

set style data lines
set bars small

plot "DATAS/tmp.csv" u 22:(95):(0.48):(90-0.01*$9*359):(90) w circ lc rgb "brown" fs solid notitle axes x2y2, \
"" u 22:(95):(0.48) w circ lc rgb "black" axes x2y2 notitle, \
"" u 1:12 w boxes lc rgb "blue" fs solid border -1 title "PrMM" axes x1y1, \
"" u 1:17 title "WSpd" axes x1y2, \
"" u 22:(70):(0.3*$17*cos(90-$16)):(0.3*$17*sin(90-$16)) with vectors lw 1 lc rgb "green" title "WDir" axes x2y2, \
"" using 1:5 title "Hum" lc rgb "cyan" axes x1y2, \
"" u 1:3:($4):3 w errorlines ls 1 pt 0 lt 1 lc rgb "red" axes x1y2 ti "Temp/DP"
#"" using 1:3 title "Temp" axes x1y2, \
#"" using 1:4 title "DewP" axes x1y2, \
